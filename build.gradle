import net.fabricmc.loom.task.RemapJarTask

plugins {
    id "dev.architectury.loom" version "${loom_version}"
	id "org.jetbrains.kotlin.jvm" version "2.1.10"
	id 'org.jetbrains.kotlin.plugin.serialization' version '2.1.10'
}

version = project.mod_version + '-' + project.minecraft_version + "-neoforge"
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(17)
    withSourcesJar()
}

repositories {
    mavenCentral()
    maven { url = "https://repo.spongepowered.org/maven" }
    maven { url = 'https://thedarkcolour.github.io/KotlinForForge/' }
    maven { url = 'https://maven.neoforged.net/releases/' }
}

loom {
	accessWidenerPath = file("src/main/resources/bahalo.accesswidener")
}

tasks.named("remapJar",RemapJarTask) {
    atAccessWideners.add("bahalo.accesswidener")
}

sourceSets {
    main {
        java { srcDirs = ["src/main/java","src/client/java"] }
        kotlin { srcDirs = ["src/main/kotlin","src/client/kotlin"] }
        resources { srcDirs = ["src/main/resources","src/client/resources"] }
    }
}

dependencies {
    minecraft "net.minecraft:minecraft:${minecraft_version}"
    neoForge "net.neoforged:neoforge:${neoforge_version}"
    mappings loom.layered {
        it.mappings("net.fabricmc:yarn:$yarn_mappings:v2")
        it.mappings("dev.architectury:yarn-mappings-patch-neoforge:1.21+build.6")
    }
    implementation "thedarkcolour:kotlinforforge-neoforge:$kff_version"
}


tasks.named('processResources', ProcessResources).configure {
    var replaceProperties = [
            minecraft_version: minecraft_version, minecraft_version_range: minecraft_version_range,
            neoforge_version: neoforge_version,
            mod_version: mod_version, kff_version: kff_version,
    ]
    inputs.properties replaceProperties

    filesMatching('META-INF/neoforge.mods.toml') {
        expand replaceProperties + [project: project]
    }
}


// Example for how to get properties into the manifest for reading at runtime.
tasks.named('jar', Jar).configure {
    from("LICENSE") {
        rename { "${it}_${archives_base_name}"}
    }
    manifest {
        attributes([
                'Specification-Title'     : archives_base_name,
                'Specification-Version'   : mod_version,
                'Implementation-Title'    : project.name,
                'Implementation-Version'  : mod_version,
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}
kotlin {
    jvmToolchain(21)
}