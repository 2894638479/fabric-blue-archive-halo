import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
	id 'fabric-loom' version "${loom_version}"
	id "me.modmuss50.mod-publish-plugin" version "0.8.1"
	id "org.jetbrains.kotlin.jvm" version "2.3.10"
	id 'org.jetbrains.kotlin.plugin.serialization' version '2.3.10'
}

version = "$mod_version+$minecraft_version+$loader"
group = project.maven_group

base.archivesName = project.archives_base_name

repositories {
	maven {
		url "https://maven.terraformersmc.com"
	}
	maven { url "https://jitpack.io" }
}

loom {
	splitEnvironmentSourceSets()

	mods {
		"blue-archive-halo" {
			sourceSet sourceSets.main
			sourceSet sourceSets.client
		}
	}

	accessWidenerPath = file("src/main/resources/bahalo.accesswidener")
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
	modImplementation "net.fabricmc:fabric-language-kotlin:${project.fabric_kotlin_version}"
	modImplementation "com.terraformersmc:modmenu:${project.mod_menu_version}"
	implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:${project.kotlin_serialization_version}"

	implementation("com.github.2894638479:KotlinMCUI:v${kotlinmcui_version}")
	modImplementation("com.github.2894638479:KotlinMCUI-backend:v${kotlinmcuibackend_version}-${loader}-${minecraft_version}")
}

processResources {
	inputs.property "version", project.version
	inputs.property "minecraft_version", project.minecraft_version
	inputs.property "fabric_kotlin_version", project.fabric_kotlin_version
	inputs.property "kotlinmcui_version", project.kotlinmcui_version
	filesMatching("fabric.mod.json") {
		expand (
                "version": inputs.properties.version,
				"minecraft_version": inputs.properties.minecraft_version,
				"fabric_kotlin_version": inputs.properties.fabric_kotlin_version,
				"kotlinmcui_version": inputs.properties.kotlinmcui_version
        )
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

tasks.withType(KotlinCompile).configureEach {
	compilerOptions.jvmTarget = JvmTarget.JVM_17
	compilerOptions.freeCompilerArgs.add('-Xcontext-parameters')
}

java {
	withSourcesJar()
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

jar {
	inputs.property "archivesName", base.archivesName

	from("LICENSE") {
		rename { "${it}_${inputs.properties.archivesName}"}
	}
}


def tag = "$mod_version-$minecraft_version-$loader"

def shouldPublishProvider = providers.exec {
	commandLine 'git', 'ls-remote', '--tags', 'origin', "refs/tags/${tag}"
}.standardOutput.asText.map { it.trim().isEmpty() }

tasks.configureEach {
	if (group == 'publishing') {
		enabled = shouldPublishProvider.get()
	}
}

publishMods {
	file = remapJar.archiveFile
	additionalFiles = files(sourcesJar.archiveFile,jar.archiveFile)
	changelog = "no changelog."
	if(mod_version.toUpperCase().contains("SNAPSHOT")) {
		type = ALPHA
	} else if(mod_version.toLowerCase().contains("alpha")) {
		type = ALPHA
	} else if(mod_version.toLowerCase().contains("beta")) {
		type = BETA
	} else {
		type = STABLE
	}
	displayName = "KotlinMCUI-backend $loader-$mod_version"
	modLoaders.add(loader)

	modrinth {
		accessToken = providers.environmentVariable("MODRINTH_TOKEN")
		projectId = "rtLQjYeH"
		optional("modmenu")
		requires("kotlinmcui","fabric-language-kotlin")
		minecraftVersionRange {
			start = minecraft_version
			end = minecraft_version
		}
	}
	curseforge {
		accessToken = providers.environmentVariable("CURSEFORGE_TOKEN")
		projectId = "1368384"
		optional("modmenu")
		requires("kotlinmcui","fabric-language-kotlin")
		clientRequired = true
		serverRequired = false
		minecraftVersionRange {
			start = minecraft_version
			end = minecraft_version
		}
	}
	github {
		accessToken = providers.environmentVariable("GITHUB_TOKEN")
		repository = "2894638479/fabric-blue-archive-halo"
		if(loader == "fabric") {
			commitish = minecraft_version
		} else {
			commitish = "$minecraft_version-$loader"
		}
		tagName = tag
	}
}